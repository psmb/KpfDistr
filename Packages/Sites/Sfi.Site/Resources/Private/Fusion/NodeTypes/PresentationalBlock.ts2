prototype(Sfi.Site:PresentationalBlock)>
prototype(Sfi.Site:PresentationalBlock) < prototype(Neos.Fusion:Array) {
    @process.tmpl = ${'<div class="medium-6 small-12 columns marginTop-double">' + value + '</div>'}
    @context.sectionNode = ${node.properties.section}
    block = Neos.Fusion:Tag {
        attributes.style = ImageUri {
            @process.tmpl = ${'background-size: contain; background: linear-gradient(rgba(0, 0, 0, 0.2), rgba(0, 0, 0, 0.5) 35%, rgba(0, 0, 0, 1) 50%), url(' + value + ')'}
            asset = ${node.properties.image}
            maximumWidth = 960
            width = 960
            maximumHeight = 640
            height = 640
            allowCropping = true
        }
        attributes.class = 'paddingTop-triple paddingHorizontal-double PresentationalBlock-mainBlock'
        content = Neos.Fusion:Array {
            title = ${'<h2 class="H1 color-white">' + sectionNode.properties.title + '</h2>'}
            description = Neos.Fusion:Tag {
                attributes.class = 'H4 color-white textShadow-deep marginVertical-double'
                content = ${q(node).property('description')}
                @process.contentElementEditable = ContentElementEditable {
                    property = 'description'
                }
            }
            button = Neos.Fusion:Tag {
                tagName = 'a'
                attributes.class = 'H4 Button color-white'
                attributes.href = NodeUri {
                    node = ${sectionNode}
                }
                content = ${q(node).property('buttonText')}
                @process.contentElementEditable = ContentElementEditable {
                    property = 'buttonText'
                }
            }
            children = ContentCollectionRenderer {
                prototype(Neos.NodeTypes:Image) {
                    width = 800
                    height = 450
                    allowCropping = true
                    imageClassName = 'border-white'
                }
            }
        }
    }

    news = Flowpack.Listable:Listable {
        @if.sectionIsSet = ${sectionNode ? true : false}
        @context.listClass = 'row small-up-1 medium-up-1 large-up-2 marginVertical-double'
        @context.itemClass = 'column'
        @context.limit = 2
        @context.paginationEnabled = ${false}
        @context.itemRenderer = 'Sfi.Site:NewsStandardShort'
        collection = ${q(sectionNode).find('[instanceof Sfi.Site:News]').get()}
    }

    @cache {
        mode = 'cached'
        entryIdentifier.node = ${node}
        entryTags {
            1 = ${'Node_' + node.identifier}
            1 = ${'DescendantOf_' + node.identifier}
            1 = ${'Node_' + sectionNode.identifier}
            1 = ${'DescendantOf_' + sectionNode.identifier}
        }
    }

    @process.contentElementWrapping = ContentElementWrapping
}
