include: OverrideStock.ts2
include: NodeTypes/**/*.ts2
include: Objects/**/*.ts2

prototype(Sfi.Site:Root) < prototype(Neos.Neos:Page) {
	head {
		titleTag.content = ${String.stripTags(q(node).property('title'))}
		stylesheets.site = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Site/Private/Partials/SharedResources.html'
			sectionName = 'stylesheets'
		}
		javascripts.headScripts = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Site/Private/Partials/SharedResources.html'
			sectionName = 'headScripts'
		}

		opengraph = Sfi.Site:Opengraph
		opengraph.@if.hasImage = ${q(node).is('[instanceof Sfi.Site:ImageMixin]')}
	}
	htmlTag.attributes.lang="ru"
	bodyTag.attributes.class = 'Page'
	bodyTag.attributes.id = ${'Page-' + q(node).property('_name')}
	body {
		templatePath = 'resource://Sfi.Site/Private/Templates/Pages/Root.html'
		partialRootPath = 'resource://Sfi.Site/Private/Partials'
		layoutRootPath = 'resource://Sfi.Site/Private/Layouts'

		mainMenu = Neos.Fusion:Collection {
			@process.tmpl = ${'<div class="MainMenu">' + value + '</div>'}
			collection = ${q(site).children('[instanceof Neos.Neos:Document]').filter('[_hiddenInIndex = FALSE]').get()}
			itemName = 'node'
			itemRenderer = Neos.Fusion:Tag {
				tagName = 'a'
				attributes.class = Neos.Fusion:RawArray {
					default = 'MainMenu-item'
					active = 'isActive'
					active.@if.active = ${documentNode == node || q(documentNode).parents('[_identifier = "' + node.identifier + '"]').count() > 0}
				}
				attributes.href = NodeUri {
					node = ${node}
				}
				content = ${node.properties.navTitle || node.properties.title}
			}
		}
		breadcrumbs = Neos.Neos:BreadcrumbMenu {
			@if.notMain = ${documentNode != site}
		}

		main = PrimaryContent {
			videosArchive {
				condition = ${request.arguments.videos}
				type = 'Sfi.Site:VideosArchive'
			}
			renderArchive {
				condition = ${request.pluginArguments.listable-paginate['@action']}
				type = 'Sfi.Site:NewsBlogArchive'
			}
			renderByType {
				condition = TRUE
				type = ${q(node).property('_nodeType.name')}
			}
		}


		javascripts.bodyScripts = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Site/Private/Partials/SharedResources.html'
			partialRootPath = 'resource://Sfi.Site/Private/Partials'
			sectionName = 'bodyScripts'
		}

		showCallout = ${q(node).is('[instanceof Sfi.Site:PageAbout]') ? false : true}

		# Cover
		title = ${node.properties.title}
		description = ${node.properties.description}
		showCover = ${q(node).is('[instanceof Sfi.Site:HasCover]') && !request.pluginArguments.listable-paginate['@action']}
		coverImage = Neos.Neos:ImageUri {
			asset = ${node.properties.imageWide}
			maximumWidth>
			width = 1168
			height = 437
			allowCropping = TRUE
			allowUpScaling = TRUE
		}

		# Editable info in header and footer
		site = ${site}
		headerInfo = ${q(site).property('headerInfo')}
		headerDescription = ${q(site).property('headerDescription')}
		address = ${q(site).property('address')}
		phone = ${q(site).property('phone')}
		email = ${q(site).property('email')}
		contactPersonName = ${q(site).property('contactPersonName')}
		contactPersonPosition = ${q(site).property('contactPersonPosition')}
		contactPersonImage = ${q(site).property('contactPersonImage')}
	}
}

root.@cache.entryIdentifier.paginationFlag = ${request.pluginArguments.listable-paginate['@action']}
root.@cache.entryIdentifier.pagination = ${request.pluginArguments.listable-paginate.currentPage}
prototype(Neos.Neos:Page).@cache.entryIdentifier.paginationFlag = ${request.pluginArguments.listable-paginate['@action']}
prototype(Neos.Neos:Page).@cache.entryIdentifier.pagination = ${request.pluginArguments.listable-paginate.currentPage}


root.fallbackPage {
	@position = 'before default'
	condition = TRUE
	type = 'Sfi.Site:Root'
}

# Disable stupid layout mechanism
root.layout>
